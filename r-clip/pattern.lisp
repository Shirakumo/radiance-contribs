#|
 This file is a part of Radiance
 (c) 2014 Shirakumo http://tymoon.eu (shinmera@tymoon.eu)
 Author: Nicolas Hafner <shinmera@tymoon.eu>
|#

(in-package #:r-clip)

;;;;;;
;; PATTERN syntax
;;
;; pattern   ::= ([domain] [port] "/" [path]) | resource
;; domain    ::= (subdomain ".")* subdomain
;; subdomain ::= alphas | subst
;; port      ::= ":" (number | subst)
;; path      ::= (string | subst)*
;; resource  ::= "<" target [":" name [";" args]] ">"
;; target    ::= alphas
;; name      ::= alphas
;; args      ::= arg*
;; arg       ::= subst | resource | ((!">"|",")*)
;; subst     ::= "{" (!"}")* "}"

(defvar *args* ())
(define-matcher letters (or (in #\a #\z) (in #\A #\Z)))
(define-matcher number (in #\0 #\9))
(define-matcher alpha (or :letters :number (is #\-)))

(defclass pattern (uri)
  ())

(defgeneric resolve (pattern &rest args)
  (:method (thing &rest args)
    (declare (ignore args))
    thing)
  (:method ((pattern pattern) &rest args)
    (let ((*args* (or args *args*)))
      (make-uri :domains (mapcar #'resolve (domains pattern))
                :port (resolve (port pattern))
                :path (if (listp (path pattern))
                          (format NIL "狺磲疸狎＇蝈箫祧疳翳疳趑弪瞟┅蝈箫祧疳翳疳趑弪瞟┅┅┅ㄤ彐躅疳蝮瀛疳趑弪篝蜷铉鏖翳戾弪孱鲩蝻铐孱篝蜷铉扉篝蝈徜疳趑弪瞟戾è篝狎痨蹴鸷轭溴┅祜镳躅糸冀戾铉翳痨蹴鸷篝蜷铉篝狎舂骘麸脲铄鳝篝狎舂眭祠轲戾鲠祯瀛扉篝蝈徜骝镯篝蜷铉痨蹴鸷篝蜷铉后翎螋篝狎舂泔祆邈ㄣ扉鸷蝈箫祧瀛鲠祯麸脲瞟滹箦翩篝狎铄鳝篝狎舂┅┅ㄤ彐躅蝈徜疳趑弪ī矧蝈徜蝈箫躜沐戾è滹磲轭蝈徜滹磲轭螬痫螋蝈徜痫螋┅疳翳蝈徜疳翳┅磲脲轭篝犷沐ю狒翦蝾轰镯衢铙滹磲轭吼矧痫螋吼狒疳翳喉狒汨弪紊泰┅ㄤ彐躅蝈徜滹磲轭ī祜镳鏖翳滹磲轭ī麒殪ㄦ躅汜祆磲脲磲翥桢矧ㄡ铢＼＼＼横祓栳┅滹麒孱ㄣ栳蚪疱咫＼ㄡ漩犷沐┅瘐箬蝈徜篚怃镯衢瞟滹磲轭螬骈钺祆蝈趱蝾滹磲轭螬┅ㄤ彐躅蝈徜篚怃镯衢ī矧蝈徜篚怏糸趱翦ㄣ镱篚礤躅糸磲脲磲翥桢铒横祓栳┅┅ㄤ彐躅蝈徜痫螋ī麒孱ㄣ栳蚪矧疱咫＼＼憨ㄡ漩犷沐换箅轲忮玳铑轭矧蝈徜篚怏糸趱翦ㄣ镱篚礤躅糸磲脲磲翥桢铒侯蹴忮颟┅┅ㄤ彐躅蝈徜疳翳ī麒孱矧ㄣ栳蚪矧疱咫＼＼ㄥ蝌矧⑿狒屮疱泗邃┅ㄡ漩犷沐换箅轲忮玳铑轭祜镳骘疱咫疱咫麒殪疱咫泔祆邈ㄣ狍疱咫ǎ茺蝈徜篚怏糸趱翦┅ㄔㄣ镱篚礤躅糸磲脲磲翥桢ㄩ＼┅┅┅┅ㄤ彐沆狍蝈箫躜沐īè翎蜱弭洪铋翎蜱呼狎珏洪铋翩矧ㄥ蝌矧⒃烈桥蝈聃轵邃横沣弩箫翎蜱弭钺礤洪铋翎蜱侯犴洪铋翩矧轰镯衢横沣弩箫钺礤ㄡ蜱洪铋翎蜱横蜱洪铋翩矧ī横沣弩箫狎珞┅ㄤ彐礤翳镤蝈箫祧è蝈箫躜沐蝈箫躜沐蝈篝狎珞戾è狎珞矧狎珞狎珞┅ㄡ痧禊＇蝈箫躜沐翎蜱弭蝈箫躜沐钺礤蝈箫躜沐磲疸狎＇蝈箫祧ㄡ蜱蝈箫躜沐┅┅ㄤ彐躅蝈徜蝈箫躜沐ī麒孱ㄣ栳蚪矧疱咫＼＼缉ㄡ漩犷沐换箅轲镳孱轭戾è盹漉戾蝈徜蝈箫躜沐翎蜱弭┅钺礤蝈徜蝈箫躜沐钺礤┅ㄡ蜱蝈徜蝈箫躜沐狎珞┅ㄡ漩犷沐换箅轲沆矬轭躅戾篌盹漉戾盹漉戾麽蝾⑽盹漉戾矧轭翦蜴徙腩秣瞵怩躞邃狍蝈箫躜沐殇孱糸骈弪轭找僧盹漉戾┅磲脲轭篝犷沐蝈箫躜沐呼狎珏盹漉戾侯犴矧钺礤轰镯衢瞟横蜱狎珞┅┅ㄤ彐躅蝈徜蝈箫躜沐钺礤ī麒孱ㄣ栳蚪疱咫＼憨ㄡ漩犷沐ㄣ镱篚礤躅糸磲脲磲翥桢ㄡ铢＼＼哗┅┅ㄤ彐躅蝈徜蝈箫躜沐翎蜱弭ī篝蜷铉躔汜箦ㄣ镱篚礤躅糸磲脲磲翥桢ㄡ铢＼＼憨┅┅ㄤ彐躅蝈徜蝈箫躜沐狎珞ī麒孱ㄣ栳蚪疱咫＼哗ㄡ漩犷沐祜镳骘疱咫疱咫躅糸矧铒疱咫ㄣ栳蚪疱咫＼京滹麒孱ㄣ栳蚪疱咫＼ㄡ漩犷沐┅泔祆邈矧蝈徜篚怏糸趱翦蝈徜蝈箫躜沐ㄣ镱篚礤躅糸磲脲磲翥桢ㄡ铢＼＼┅┅┅ㄤ彐沆狍痨徙彖镬溴īè鲠洪铋翎蜱忽狎洪铋翩矧ㄥ蝌矧⒅烈蝈聃轵邃横沣弩箫鲠颟┅ㄤ彐礤翳镤蝈箫祧è痨徙彖镬溴痨徙彖镬溴颟蝈篝狎珞ㄤ邈灬蝈ㄩ珙矧狎珞┅戾è鲠鲠痨徙彖镬溴颟┅ㄥ豉疱汜箦鲠ㄦ轼铛铘鲠狎珞┅脲黠蜾ㄧ弭狎珞鲠颟┅┅ㄤ彐躅蝈徜篚怏糸趱翦ī麒孱ㄣ栳蚪矧疱咫＼＼ㄡ漩犷沐换箅轲镳孱轭戾舄è泔铘孱趔ㄣ镱篚礤躅糸磲脲磲翥桢ㄩ＼┅┅脲黠蜾矧ㄩ珙矧瀛弪蝻蝮疳蝮瀛轭翦珏泔铘孱趔┅ㄩ铘弪篝蜷铉躔汜箦泔铘孱趔⑺刨紫夷┅┅ㄡ漩犷沐换箅轲沆矬轭磲脲轭篝犷沐ю灬沐栾熹弪忽狎脲黠蜾┅┅